#!/usr/bin/env zsh

#######################################################################
#                               ZSHENV                                #
#######################################################################

###############################
#  Platform-specific Sources  #
###############################

# Determine platform first
if [[ -z ${ZSH_UNAME} ]]; then
  ZSH_UNAME=$(uname)
  export ZSH_UNAME
fi

if [[ -f "$HOME/.zsh/zshenv_$ZSH_UNAME" ]]; then
  source "$HOME/.zsh/zshenv_$ZSH_UNAME"
fi

#######################
#  Personal Settings  #
#######################

export EDITOR=nvim
export LANG=en_US.UTF-8
export COLORFGBG="7;0"

####################
#  Personal Paths  #
####################

if [[ -d "$HOME/bin" ]]; then
  path=($HOME/bin $path)
fi

if [[ -d "$HOME/local/bin" ]]; then
  path=($HOME/local/bin $path)
fi

if [[ -d "$HOME/.local/bin" ]]; then
  path=($HOME/.local/bin $path)
fi

###############
#  Dircolors  #
###############

if (( $+commands[dircolors] )); then
  eval "$(dircolors)"
  combined_colors="$LS_COLORS"
  for file in "$HOME"/.dircolors*(N); do
    if [[ -f $file ]]; then
      eval "$(dircolors --sh "$file")"
      combined_colors+=":$LS_COLORS"
    fi
  done
  export LS_COLORS="${combined_colors%:}"
  unset combined_colors
fi

#####################
#  C-Include Paths  #
#####################

# Prevent pkg-config from omitting paths in C_INCLUDE_PATH
export PKG_CONFIG_ALLOW_SYSTEM_CFLAGS=1

if (( $+commands[pkg-config] )); then
  C_INCLUDE_PATH=$(pkg-config --variable=includedir libpcre):$C_INCLUDE_PATH
  C_INCLUDE_PATH=$(pkg-config --variable=includedir gsl):$C_INCLUDE_PATH
fi

if (( $+commands[ocamlc] )); then
  C_INCLUDE_PATH="$(ocamlc -where):$C_INCLUDE_PATH"
fi

export C_INCLUDE_PATH

##########
#  Rust  #
##########

if [[ -f "$HOME/.cargo/env" ]]; then
  source "$HOME/.cargo/env"
fi

##########
#  OPAM  #
##########

if [[ -r "$HOME/.opam/opam-init/init.zsh" ]]; then
  source "$HOME/.opam/opam-init/init.zsh" >/dev/null 2>/dev/null || true
fi

############
#  Docker  #
############

if [[ -d "$HOME/.docker/bin" ]]; then
  path=($HOME/.docker/bin $path)
fi

#########################
#  Save PATH on Darwin  #
#########################

# Necessary on macOS due to Apple path_helper path manipulation
if [[ $ZSH_UNAME == "Darwin" ]]; then
  export ORIGINAL_PATH=$PATH
fi
